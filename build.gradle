plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven { url "https://api.modrinth.com/maven" }
	maven { url "https://maven.kyrptonaught.dev" }
	maven { url "https://maven.wispforest.io" }
    mavenCentral()
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"wildlight" {
			sourceSet sourceSets.client
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // PhysicsMod
    modImplementation files("libs/physics-mod-3.0.24-mc-1.21.1-fabric.jar")

    // Deeper and Darker (and its dependencies)
	modImplementation "maven.modrinth:deeperdarker:${project.deeperdarker_version}"
	include modImplementation("net.kyrptonaught:customportalapi:${project.customportalapi_version}")
	modImplementation "io.wispforest:owo-lib:${project.owo_version}"

    // Friends and Foes (and its dependencies)
    modImplementation "maven.modrinth:friends-and-foes:${project.friendsandfoes_version}"
    modImplementation files("libs/resourcefullib-fabric-1.21-3.0.12.jar")
    modImplementation files("libs/yet_another_config_lib_v3-3.6.5+1.21.1-fabric.jar")

    // Immersive Armors
    modCompileOnly "maven.modrinth:immersive-armors:${project.immersive_armors_version}"
    modRuntimeOnly "maven.modrinth:immersive-armors:${project.immersive_armors_version}"

    // Detail Armor Bar Compat
    modImplementation "maven.modrinth:detailab-compat:${project.detailab_compat_version}"
    modImplementation "maven.modrinth:detail-armor-bar:${project.detailab_version}"

	// Visual Overhaul
	modImplementation "maven.modrinth:visual-overhaul:${project.visualoverhaul_version}"
	modImplementation "maven.modrinth:midnightlib:${project.midnightlib_version}"
}

processResources {
	def replaceProperties = new HashMap<String, String>()

	project.properties.each { key, value ->
		if (value instanceof String) {
			replaceProperties.put(key, value)
		}
	}

	inputs.properties replaceProperties

	filesMatching('fabric.mod.json') {
		expand replaceProperties
	}
}

tasks.named("processClientResources") {
	def replaceProperties = new HashMap<String, String>()

	project.properties.each { key, value ->
		if (value instanceof String) {
			replaceProperties.put(key, value)
		}
	}

	inputs.properties replaceProperties

	filesMatching('fabric.mod.json') {
		expand replaceProperties
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

tasks.javadoc {
    source = sourceSets.client.allJava
}

java {
	withSourcesJar()
    withJavadocJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

tasks.withType(Javadoc).configureEach {
	enabled = false
}